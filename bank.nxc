#define ACCOUNTS_FILENAME "konten.txt"
#define CLEARLINE(L) TextOut(0,L,"                ");
byte pressedButton;
int selectButton;
//account limit set to 32 because cards have 5 fields
int accounts=32;

//general account structure
struct account {
int accountno;
int pinno;
string lastname;
string firstname;
string title;
int amount;
bool inactive;
int wrongpin;
};

account accountlist[accounts];
bool finish=FALSE;

// === Functions & Operations ===

// taken from NXTmenu.nxc
void waitButton() {
byte wbIndex=BTNRIGHT;
while (wbIndex <= BTNCENTER)   {
    if (ButtonPressed(wbIndex,false) != 0)
            break;
            wbIndex++;
    if (wbIndex > BTNCENTER)
            wbIndex = BTNRIGHT;
}
until(!ButtonPressed(wbIndex,FALSE));
pressedButton = wbIndex;
}

int getAccountNo() {
//generic function to let user select account no with L/R arrows on nxt
//and confirm with the orange button
bool accstate=TRUE;
//default to test account: 3
int accountno=3;

while (accstate) {
    TextOut(0,LCD_LINE1,"Account number:");
    NumOut(0,LCD_LINE2,accountno);
    waitButton();
    switch (pressedButton) {
        case BTNLEFT:
            accountno--;
            break;
        case BTNRIGHT:
            accountno++;
            break;
        case BTNCENTER:
            accstate=FALSE;
            break;
    }
}

return accountno;

}


string limitnumber() {
//FN to call to get a random digit, for simplicity we assume
//this to be a true random number generator
int check=Random(9);
//reject values if not 1-4 due to limited pin field & try again
while (check < 1 || check > 4) {
    check=Random(9);
}
return NumToStr(check);
}

void createAccount() {
//FN to create an account or reset the pin on an existing one,
// since we differentiate created accounts by having a pin number
int accountno=getAccountNo();
//TODO:VERIFY PIN CHANGE KEEPS AMOUNT
account pending=accountlist[accountno];
string a=limitnumber();
string b=limitnumber();
string c=limitnumber();
string d=limitnumber();

string trypin=a+b+c+d;

pending.accountno = accountno;
pending.pinno = StrToNum(trypin);
pending.wrongpin = 0;
pending.inactive = FALSE;

ClearScreen();
TextOut(0,LCD_LINE1,"Create account/");
TextOut(0,LCD_LINE2,"new PIN for:");
//TODO:visible well?
NumOut(50,LCD_LINE2,accountno);
TextOut(0,LCD_LINE4,"Arrow: abort");
TextOut(0,LCD_LINE5,"Orange: continue");
    waitButton();
    switch (pressedButton) {
    case BTNLEFT:
        TextOut(0,LCD_LINE6,"aborted");
        break;
    case BTNRIGHT:
        TextOut(0,LCD_LINE6,"aborted");
        break;
    case BTNCENTER:
        ClearScreen();
        TextOut(0,LCD_LINE1,"ACCOUNT:");
        NumOut(50,LCD_LINE1,accountno);
        TextOut(0,LCD_LINE2,"PIN:");
        NumOut(50,LCD_LINE2,pending.pinno);
        accountlist[accountno]=pending;
        break;
    }
Wait(3000);

}

void closeAccount() {
//if the balance in the selected account is 0, set to inactive
int accountno=getAccountNo();
account closeme=accountlist[accountno];
int temp=closeme.amount;
if (temp > 0) {
    ClearScreen();
    TextOut(0,LCD_LINE1,"Still money in");
    TextOut(0,LCD_LINE2,"account withdraw");
    TextOut(0,LCD_LINE2,"it first");
    NumOut(0,LCD_LINE3,closeme.amount);
    Wait(3000);
} else {
    TextOut(0,LCD_LINE4,"Arrow: abort");
    TextOut(0,LCD_LINE5,"Orange: continue");
    waitButton();
    switch (pressedButton) {
    case BTNLEFT:
        TextOut(0,LCD_LINE6,"aborted");
        break;
    case BTNRIGHT:
        TextOut(0,LCD_LINE6,"aborted");
        break;
    case BTNCENTER:
        closeme.inactive=TRUE;
        accountlist[accountno]=closeme;
        ClearScreen();
        TextOut(0,LCD_LINE1,"ACCOUNT:");
        NumOut(50,LCD_LINE1,accountno);
        TextOut(0,LCD_LINE2,"Set inactive");
        break;
    }
    Wait(3000);

}

}

void showAccount() {
//just show one account's details
int accountno=getAccountNo();
account showaccount=accountlist[accountno];

TextOut(0,LCD_LINE1,"Konto:");
NumOut(45,LCD_LINE1,showaccount.accountno);

TextOut(0,LCD_LINE2,"Pin:");
NumOut(45,LCD_LINE2,showaccount.pinno);


TextOut(0,LCD_LINE3,"Amt:");
NumOut(45,LCD_LINE3,showaccount.amount);

TextOut(0,LCD_LINE4,"WPin:");
NumOut(45,LCD_LINE4,showaccount.wrongpin);
tp=SubStr(request,3,4);

TextOut(0,LCD_LINE5,"Inv:");
NumOut(45,LCD_LINE5,showaccount.inactive);

Wait (6000);
}

void additionalFunds() {
//add funds to selected account, increase/decrease by 20
//with L/R arrows on NXT
int accountno=getAccountNo();
account addfunds=accountlist[accountno];
int amount=100;
bool addstate=TRUE;
while (addstate) {
    ClearScreen();
    TextOut(0,LCD_LINE1,"Add funds:");
    NumOut(0,LCD_LINE2,amount);
    waitButton();
    switch (pressedButton) {
        case BTNRIGHT:
        amount+=20;
        break;

        case BTNLEFT:
        amount -=20;
        break;

        case BTNCENTER:
        ClearScreen();
        if (addfunds.inactive==FALSE) {
            addfunds.amount+=amount;
            accountlist[accountno]=addfunds;
            TextOut(0,LCD_LINE1,"Updated, balance:");
            NumOut(0,LCD_LINE2,addfunds.amount);
            Wait(2000);
        } else {
            TextOut(0,LCD_LINE1,"Sorry, inactive account");
            Wait(2000);
        }
        addstate=FALSE;
        break;
    }
}

}



// === Network Communication ===

task networkListener() {

while(true) {
    int connection=0;
    string tc,tp,ta,requesta,requestb,requestc,request;

    //loop until either in mailbox one,two,or three a message is found
    until ((ReceiveRemoteString(1,true,requesta) == NO_ERR) ||
        (ReceiveRemoteString(2,true,requestb) == NO_ERR) ||
        (ReceiveRemoteString(3,true,requestc) == NO_ERR));

    //determine the connection based upon in which mailbox the message was
    if (StrLen(requesta) > 9) {
        request=requesta;
        connection=1;
    } else if (StrLen(requestb) > 9) {
        request=requestb;
        connection=2;
    } else if (StrLen(requestc) > 9 ) {
        request=requestc;
        connection=3;
    }
    //extract two digit account number
    tc=SubStr(request,0,2);
    int scard=StrToNum(tc);
    //if connection and card not 0
    if ((scard!=0) && (connection!=0)) {
        ClearScreen();

        //extract pin and amount and display
        TextOut(0,LCD_LINE2,"Konto:");
        NumOut(45,LCD_LINE2,scard);
        tp=SubStr(request,3,4);
        int spin=StrToNum(tp);
        TextOut(0,LCD_LINE3,"Pin:");
        NumOut(45,LCD_LINE3,spin);
        ta=SubStr(request,9,4);
        int samount=StrToNum(ta);
        TextOut(0,LCD_LINE4,"Amt:");
        NumOut(45,LCD_LINE4,samount);

        //compare and display to accountlist
        account person;
        person=accountlist[scard];
        TextOut(0,LCD_LINE5,"DB P:");
        NumOut(45,LCD_LINE5,person.pinno);
        TextOut(0,LCD_LINE6,"DB A:");
        NumOut(45,LCD_LINE6,person.amount);

        if (person.pinno==0) {
            //no pin number set, not a valid account
            PlayTone(200,100);
            Wait(200);
            SendRemoteString(connection,7,"card");
            TextOut(0,LCD_LINE7,"card");

        } else {
        //it has a pin number
        if (spin == person.pinno) {
            //correct pin number
            if (person.inactive==FALSE) {
                //also account active
                PlayTone(200,100);
                Wait(200);
                //active and right pin>>reset wrong pin count
                person.wrongpin=0;
                accountlist[scard]=person;
                if (samount <= person.amount) {
                    //enough funds, dispense!
                    PlayTone(300,100);
                    //decrease account balance
                    person.amount-=samount;
                    //show new balance next to previous balance
                    NumOut(75,LCD_LINE6,person.amount);
                    SendRemoteString(connection,7,"ack");
                    accountlist[scard]=person;
                } else {
                    //insufficient funds
                    PlayTone(100,50);
                    Wait(200);
                    SendRemoteString(connection,7,"funds");
                    TextOut(0,LCD_LINE7,"funds");
                }
            } else {
                //account inactive
                PlayTone(100,50);
                Wait(200);
                SendRemoteString(connection,7,"inactive");
                TextOut(0,LCD_LINE7,"inactive");
            }
        } else {
            //wrong pin number
            //increase person's wrong pin number count
            person.wrongpin++;
            accountlist[scard]=person;
            if (person.wrongpin>=3) {
                //already three times, keep card
                PlayTone(100,50);
                Wait(200);
                SendRemoteString(connection,7,"kill");
                TextOut(0,LCD_LINE7,"kill");
            } else {
                //less than three wrong tries, give card back
                PlayTone(100,50);
                Wait(200);
                SendRemoteString(connection,7,"pin");
                TextOut(0,LCD_LINE7,"pin");
            }

        }
    Wait(1000);

    }
    } else {
        SendRemoteString(connection,7,"card");
    }

    requesta="a";
    requestb="a";
    requestc="a";
    request="a";

}

}

// === File I/O ===


void startfile() {

byte handle;
int fsize,accheck;
bool eof = false;
string buf,a;

//open the file and read until end of file
if (OpenFileRead(ACCOUNTS_FILENAME,fsize,handle) == NO_ERR) {
//based on the NXC_Tutorial.pdf
    until (eof == TRUE) {
        //simply redo the structure from shutdownFile()
        account readperson;
        ReadLnString(handle,a);
        readperson.accountno=StrToNum(a);
        ReadLnString(handle,a);
        readperson.pinno=StrToNum(a);
        ReadLnString(handle,readperson.lastname);
        ReadLnString(handle,readperson.firstname);
        ReadLnString(handle,readperson.title);
        ReadLnString(handle,a);
        readperson.amount=StrToNum(a);
        ReadLnString(handle,a);
        readperson.inactive=StrToNum(a);
        ReadLnString(handle,a);
        readperson.wrongpin=StrToNum(a);

        //eof determined by having the 'spacer' line be a safe throwaway here
        if(ReadLnString(handle,buf) != NO_ERR) eof = true;
        accheck=readperson.accountno;
        if (accheck != 0) {
        TextOut(0,LCD_LINE1,"Load:");
        NumOut(50,LCD_LINE1,accheck);
        NumOut(50,LCD_LINE2,readperson.amount);
        NumOut(50,LCD_LINE3,readperson.pinno);
        NumOut(50,LCD_LINE4,readperson.wrongpin);
        accountlist[readperson.accountno]=readperson;
        Wait(700);
        ClearScreen();
        }
    }
if (CloseFile(handle) != NO_ERR)
TextOut(0,LCD_LINE1,"Loose handle!");

} else {
    TextOut(0,LCD_LINE1,"Cannot open file");
}
Wait(2000);
}

void shutdownfile() {

//Write a file to disk with all accounts where a pin number is set
//the latter prevents from writing 20+ empty accounts

byte handle;
int bwritten;
string a;
int w,x,y,z;
//delete file to be on the save side
if (DeleteFile(ACCOUNTS_FILENAME) != NO_ERR) {
TextOut(0,LCD_LINE6,"Not deleted");
Wait(1000);
}
//create file again
if (CreateFile(ACCOUNTS_FILENAME,1024,handle) == NO_ERR) {

    for (int i=0;i<accounts;i++) {
        account writeperson=accountlist[i];
        x=writeperson.pinno;
        if (x != 0) {
            //use WriteLnString for all values because WriteLn
            //seems to dump binary data in the file and makes it
            //unreadable
            a=NumToStr(writeperson.accountno);
            WriteLnString(handle,a,bwritten);
            a=NumToStr(writeperson.pinno);
            WriteLnString(handle,a,bwritten);
            WriteLnString(handle,writeperson.lastname,bwritten);
            WriteLnString(handle,writeperson.firstname,bwritten);
            WriteLnString(handle,writeperson.title,bwritten);
            a=NumToStr(writeperson.amount);
            WriteLnString(handle,a,bwritten);
            a=NumToStr(writeperson.inactive);
            WriteLnString(handle,a,bwritten);
            a=NumToStr(writeperson.wrongpin);
            WriteLnString(handle,a,bwritten);
            WriteLnString(handle,"spacer",bwritten);
            TextOut(0,LCD_LINE1,"Saving:");
            NumOut(50,LCD_LINE1,writeperson.accountno);
            NumOut(50,LCD_LINE3,writeperson.amount);
            NumOut(50,LCD_LINE2,writeperson.pinno);
            NumOut(50,LCD_LINE4,writeperson.wrongpin);
            Wait(700);
            ClearScreen();
        }
        x=0;
    }
    ClearScreen();
    TextOut(0,LCD_LINE1,"Saved data");
    if (CloseFile(handle) == NO_ERR) {
    }
    else {
    TextOut(0,LCD_LINE2,"Loose handle!");
    PlayTone(500,100);
    }
} else {
    TextOut(0,LCD_LINE1,"Cannot open file");
    PlayTone(500,100);
}

TextOut(0,LCD_LINE4,"DONE");
Wait(1000);
}

// === UI ===

void menu() {

bool mstate=TRUE;
int i=0;
int accountno=0;

while (mstate) {
//menu that call maintenance functions until mstate
//set to false by shutdown command
    if (i > 4) i=0;
    if (i < 0) i=4;
    if (i==0) {
        ClearScreen();
        Textout(0,LCD_LINE1,"Show account");
    } else if (i==1) {
        ClearScreen();
        Textout(0,LCD_LINE1,"Add funds");
    } else if (i==2) {
        ClearScreen();
        Textout(0,LCD_LINE1,"Create/PIN");
    } else if (i==3) {
        ClearScreen();
        Textout(0,LCD_LINE1,"Close account");
    } else if (i==4) {
        ClearScreen();
        Textout(0,LCD_LINE1,"Shutdown");
    }

    waitButton();
    switch (pressedButton) {
    case BTNRIGHT: i++;
        break;
    case BTNLEFT: i--;
        break;
    case BTNCENTER:
        if (i==0) {
            showAccount();
        } else if (i==1) {
            additionalFunds();
        } else if (i==2) {
            createAccount();
        } else if (i==3) {
            closeAccount();
        } else if (i==4) {
            mstate=false;
            shutdownfile();
        }
        break;
    }
}

}

// ===Main===

task main() {
SetBrickDataName("HEMITO");
startfile();
//start the seperate network listener task
start networkListener;
//start the menu
menu();
//once menu stopped, stop all tasks (networkListener)
StopAllTasks();

}
